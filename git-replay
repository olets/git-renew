#!/usr/bin/env zsh

# git-replay
# copyright 2020 Henry Bley-Vroman
#
# Automate Git rebases and stage creation
# https://github.com/olets/git-replay

__git_replay_abort() {
	if [[ -f $action_path ]]; then
		case $(head -1 $action_path) in
			merge)
				git merge --abort
				;;
			rebase)
				git rebase --abort
				;;
		esac
	fi
	rm -rf $dir
}

__git_replay_add_rebase() {
	local branch
	local upstream

	[[ $# < 2 ]] && __git_replay_print_error "Requires two arguments (upstream and branch)"

	branch=$2
	upstream=$1

	'command' 'yq' w -i $file "rebase.${upstream}[+]" $branch
}

__git_replay_add_rebase_onto() {
	local branch
	local upstream
	local start_point

	[[ $# < 3 ]] && __git_replay_print_error "Requires three arguments (newbase, upstream, and branch)"

	branch=$3
	newbase=$1
	upstream=$2

	'command' 'yq' w -i $file "rebase-onto.$newbase.${upstream}[+]" $branch
}

__git_replay_add_stage() {
	local branch
	local upstream
	local start_point

	[[ $# < 3 ]] && __git_replay_print_error "Requires three arguments (starting point, upstream, and branch)"

	branch=$3
	start_point=$1
	upstream=$2

	'command' 'yq' w -i $file "stage.$start_point.${upstream}[+]" $branch
}

__git_replay_backup_delete() {
	local branch
	local backup_branch
	local -a backup_branches

	backup_branches=( ${(f)"$(git for-each-ref --format='%(refname:short)' refs/heads/$BACKUP_PREFIX)"} )
	[[ -z $backup_branches ]] && __git_replay_print_warning "No backup branches found" && return

	__git_replay_print_log "Deleting backups"

	for backup_branch in $backup_branches; do
		branch=${backup_branch#$BACKUP_PREFIX/}

		if ! git show-ref --verify --quiet refs/heads/$branch; then
			mkdir -p $dir
			touch $warning_path

			'builtin' 'echo' "No branch \`$branch\` found. Not deleting the backup in case it is needed.\nTo delete the backup, run\n        git branch -D $backup_branch" >> $warning_path
		else
			if (( quiet )); then
				git branch --quiet -D $backup_branch
			else
				'builtin' 'print' "git branch --quiet -D $backup_branch"
				git branch -D $backup_branch
			fi
		fi
	done
}

__git_replay_backup_restore() {
	local branch
	local backup_branch
	local -a backup_branches

	backup_branches=( ${(f)"$(git for-each-ref --format='%(refname:short)' refs/heads/$BACKUP_PREFIX)"} )
	[[ -z $backup_branches ]] && __git_replay_print_warning "No backup branches found" && return

	__git_replay_print_log "Restoring backed up branches"

	for backup_branch in $backup_branches; do
		branch=${backup_branch#$BACKUP_PREFIX/}

		if git show-ref --verify --quiet refs/heads/$branch; then
			__git_replay_print_log "Restoring $branch"

			if (( quiet )); then
				git checkout --quiet -B $branch $backup_branch
			else
				'builtin' 'print' "git checkout -B $branch $backup_branch"
				git checkout -B $branch $backup_branch
			fi
		fi
	done

	__git_replay_backup_delete
}

__git_replay_delete_rebase() {
	local branch
	local upstream

	(( $# )) || __git_replay_print_error "Requires one to two arguments (upstream, branch)"

	branch=$2
	upstream=$1

	if [[ -n $branch ]]; then
		'command' 'yq' d -i $file rebase.$upstream.$branch
		[[ -z $('command' 'yq' r $file rebase.$upstream.\*) ]] && 'command' 'yq' d -i $file rebase.$upstream
	else
		'command' 'yq' d -i $file rebase.$upstream
	fi
}

__git_replay_delete_rebase_onto() {
	local branch
	local newbase
	local upstream

	(( $# )) || __git_replay_print_error "Requires one to three arguments (newbase, upstream, and branch)"

	branch=$3
	newbase=$1
	upstream=$2

	if [[ -n $branch ]]; then
		'command' 'yq' d -i $file rebase-onto.$newbase.$upstream.$branch

		[[ -z $('command' 'yq' r $file rebase-onto.$newbase.$upstream.\*) ]] && 'command' 'yq' d -i $file rebase-onto.$newbase.$upstream
		[[ -z $('command' 'yq' r $file rebase-onto.$newbase.\*) ]] && 'command' 'yq' d -i $file rebase-onto.$newbase
	elif [[ -n $upstream ]]; then
		'command' 'yq' d -i $file rebase-onto.$newbase.$upstream

		[[ -z $('command' 'yq' r $file rebase-onto.$newbase.\*) ]] && 'command' 'yq' d -i $file rebase-onto.$newbase
	else
		'command' 'yq' d -i $file rebase-onto.$newbase
	fi
}

__git_replay_delete_stage() {
	local branch
	local start_point
	local upstream

	(( $# )) || __git_replay_print_error "Requires one to three arguments (starting point, upstream, and branch)"

	branch=$3
	start_point=$1
	upstream=$2

	if [[ -n $branch ]]; then
		'command' 'yq' d -i $file stage.$start_point.$upstream.$branch

		[[ -z $('command' 'yq' r $file stage.$start_point.$upstream.\*) ]] && 'command' 'yq' d -i $file stage.$start_point.$upstream
		[[ -z $('command' 'yq' r $file stage.$start_point.\*) ]] && 'command' 'yq' d -i $file stage.$start_point
	elif [[ -n $upstream ]]; then
		'command' 'yq' d -i $file stage.$start_point.$upstream

		[[ -z $('command' 'yq' r $file stage.$start_point.\*) ]] && 'command' 'yq' d -i $file stage.$start_point
	else
		'command' 'yq' d -i $file stage.$start_point
	fi
}

__git_replay_diverged $branch() {
	local branch
	local sha
	local remote_sha

	branch=$1
	sha=$(git rev-parse $branch)
	remote_sha=$(git rev-parse $branch@{u} 2>/dev/null)

	[[ -n $sha && -n $remote_sha && $sha != $remote_sha ]]
}

__git_replay_do_todo() {
	local -a cmd_words

	cmd_words=( ${(z)1} )

	case $cmd_words[1] in
		merge)
			local branch
			local commit
			local -i the_exit_code

			branch=$cmd_words[3]
			commit=$cmd_words[2]

			touch $action_path
			'builtin' 'echo' merge > $action_path

			__git_replay_print_log "Merging $commit into $branch"

			if ! (( dry_run )); then
				if (( quiet )); then
					git merge --quiet --rerere-autoupdate --no-ff --no-edit $commit
					the_exit_code=$?
				else
					'builtin' 'print' "git merge --rerere-autoupdate --no-ff --no-edit $commit"
					git merge --rerere-autoupdate --no-ff --no-edit $commit
					the_exit_code=$?
				fi

				(( the_exit_code )) && __git_replay_merge_continue $commit $branch $the_exit_code
			fi

			rm $action_path
			;;
		merge-continue)
			local branch
			local commit

			branch=$cmd_words[3]
			commit=$cmd_words[2]

			__git_replay_print_log "Continuing merging $commit into $branch"
			git rerere && __git_replay_merge_continue $commit $branch

			rm $action_path
			;;
		rebase)
			local branch
			local -i the_exit_code
			local upstream

			branch=$cmd_words[3]
			upstream=$cmd_words[2]

			touch $action_path
			'builtin' 'echo' rebase > $action_path

			if (( back_up )); then
				__git_replay_print_log "Backing up $branch"

				if ! (( dry_run )); then
					if (( quiet )); then
						git branch --quiet --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					else
						'builtin' 'print' "git branch --force $BACKUP_PREFIX/$branch $branch"
						git branch --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					fi
				fi
			fi

			__git_replay_print_log "Rebasing $branch off $upstream"

			if ! (( dry_run )); then
				if (( quiet )); then
					git rebase --quiet --rerere-autoupdate $upstream $branch
					the_exit_code=$?
				else
					'builtin' 'print' "git rebase --rerere-autoupdate $upstream $branch"
					git rebase --rerere-autoupdate $upstream $branch
					the_exit_code=$?
				fi

				(( the_exit_code )) && __git_replay_rebase_continue $the_exit_code $upstream $branch

				__git_replay_log_divergence $branch
			fi

			rm $action_path
			;;
		rebase-continue)
			local branch
			local newbase
			local upstream

			branch=$cmd_words[3]
			newbase=$cmd_words[4]
			upstream=$cmd_words[2]

			if [[ -n $newbase ]]; then
				__git_replay_print_log "Continuing rebasing $branch onto $newbase from $upstream"
			else
				__git_replay_print_log "Continuing rebasing $branch off $upstream"
			fi

			if ! (( dry_run )); then
				(( quiet )) || 'builtin' 'print' "git rerere"
				git rerere

				__git_replay_rebase_continue 0 $upstream $branch
				__git_replay_log_divergence $branch
			fi

			rm $action_path
			;;
		rebase-onto)
			local branch
			local -i the_exit_code
			local newbase
			local upstream

			branch=$cmd_words[4]
			newbase=$cmd_words[2]
			upstream=$cmd_words[3]

			touch $action_path
			'builtin' 'echo' rebased > $action_path

			if (( back_up )); then
				__git_replay_print_log "Backing up $branch"

				if ! (( dry_run )); then
					if (( quiet )); then
						git branch --quiet --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					else
						'builtin' 'print' "git branch --force $BACKUP_PREFIX/$branch $branch"
						git branch --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					fi
				fi
			fi

			__git_replay_print_log "Rebasing $branch onto $newbase from $upstream"

			if ! (( dry_run )); then
				if (( quiet )); then
					git rebase --quiet --rerere-autoupdate --onto $newbase $upstream $branch
					the_exit_code=$?
				else
					'builtin' 'print' "git rebase --rerere-autoupdate --onto $newbase $upstream $branch"
					git rebase --rerere-autoupdate --onto $newbase $upstream $branch
					the_exit_code=$?
				fi

				(( the_exit_code )) && __git_replay_rebase_continue $the_exit_code $upstream $branch $newbase

				__git_replay_log_divergence $branch
			fi

			rm $action_path
			;;
		stage-end)
			local branch

			branch=$cmd_words[2]

			(( dry_run )) || __git_replay_log_divergence $branch
			;;
		stage-start)
			local branch
			local -i the_exit_code
			local start_point

			branch=$cmd_words[2]
			start_point=$cmd_words[3]

			if (( back_up )); then
				__git_replay_print_log "Backing up $branch"

				if ! (( dry_run )); then
					if (( quiet )); then
						git branch --quiet --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					else
						'builtin' 'print' "git branch --force $BACKUP_PREFIX/$branch $branch"
						git branch --force $BACKUP_PREFIX/$branch $branch || __git_replay_handle_failed_backup $branch
					fi
				fi
			fi

			__git_replay_print_log "Resetting $branch to $start_point"

			if ! (( dry_run )); then
				if (( quiet )); then
					git checkout --quiet -B $branch $start_point
					the_exit_code=$?
				else
					'builtin' 'print' "git checkout -B $branch $start_point"
					git checkout -B $branch $start_point
					the_exit_code=$?
				fi
			fi

			(( the_exit_code )) && __git_replay_print_error "Staging $branch failed. Does \`$start_point' exist?"
			;;
		*)
			__git_replay_print_error "git-replay can not run the todo \`$todo'"
			;;
	esac
}

__git_replay_handle_failed_backup() {
	local branch

	branch=$1

	'builtin' 'echo' "The branch \`$branch' was not backed up. Perhaps it did not exist at the time of backup?" >> $warning_path
}

__git_replay_log_divergence() {
	local branch

	branch=$1

	__git_replay_diverged $branch || return

	mkdir -p $dir
	touch $warning_path
	'builtin' 'echo' "$branch has diverged from its upstream" >> $warning_path
}

__git_replay_main() {
	local -i continue
	local -i dediverge
	local -i force_dediverge
	local -i no_color
	local opt
	local -i queue_rebases
	local -i queue_rebase_ontos
	local -i queue_stages
	local -a todos

	queue_rebases=1
	queue_rebase_ontos=1
	queue_stages=1

	for opt in $@; do
		[[ $opt == "--no-color" ]] && no_color=1
	done

	if ! (( no_color )); then
		autoload -U colors && colors
		color=(
			[error]="%F{red}"
			[success]="%F{green}"
			[log]="%F{blue}"
			[warning]="%F{yellow}"
		)
	fi

	while (( $# )); do
		case $1 in
			--abort)
				__git_replay_abort
				return
				;;
			--continue)
				[[ -f $todo_path ]] || __git_replay_print_error "fatal: No replay in progress?"
				continue=1
				queue_rebase_ontos=0
				queue_rebases=0
				queue_stages=0
				shift
				;;
			--back-up)
				back_up=1
				shift
				;;
			--dry-run)
				dry_run=1
				shift
				;;
			--file)
				file=$2
				shift 2
				;;
			--force)
				force_dediverge=1
				shift
				;;
			--help|\
			help)
				'command' 'man' git-replay 2>/dev/null || 'command' 'cat' ${SOURCE_PATH}/man/git-replay.txt | 'command' 'less' -F
				return
				;;
			--no-color)
				shift
				;;
			-q|\
			--quiet)
				quiet=1
				shift
				;;
			-qq|\
			--quieter)
				quiet=1
				quieter=1
				shift
				;;
			--skip)
				[[ -f $todo_path ]] || __git_replay_print_error "fatal: No replay in progress?"
				continue=1
				queue_rebase_ontos=0
				queue_rebases=0
				queue_stages=0
				__git_replay_pop_todo
				shift
				;;
			-v|\
			--version)
				'builtin' 'printf' "git-replay version %s\n" "$VERSION"
				return
				;;
			add)
				shift
				case $1 in
					rebase)
						shift
						__git_replay_add_rebase $@
						return
						;;
					rebase-onto)
						shift
						__git_replay_add_rebase_onto $@
						;;
					stage)
						shift
						__git_replay_add_stage $@
						return
						;;
					*)
						__git_replay_print_error "git-replay 'add' requires 'rebase', 'rebase-onto', or 'stage'"
						return
						;;
				esac
				return
				;;
			backup-delete)
				__git_replay_backup_delete
				__git_replay_warnings && rm $warning_path
				return
				;;
			backup-restore)
				__git_replay_backup_restore
				__git_replay_warnings && rm $warning_path
				return
				;;
			delete)
				shift
				case $1 in
					rebase)
						shift
						__git_replay_delete_rebase $@
						;;
					rebase-onto)
						shift
						__git_replay_delete_rebase_onto $@
						;;
					stage)
						shift
						__git_replay_delete_stage $@
						;;
					*)
						__git_replay_print_error "git-replay 'delete' requires 'rebase', 'rebase-onto', or 'stage'"
						;;
				esac
				return
				;;
			dediverge)
				dediverge=1
				shift
				;;
			# help -- see --help
			rebase)
				queue_rebase_ontos=0
				queue_stages=0
				shift
				;;
			rebase-onto)
				queue_rebases=0
				queue_stages=0
				shift
				;;
			stage)
				queue_rebases=0
				queue_rebase_ontos=0
				shift
				;;
			*)
				__git_replay_print_error "git-replay unknown option"
				;;
		esac
	done

	if ! [[ -f $file ]]; then
		__git_replay_print_error "Could not find the file $file"
	fi

	if [[ -f $todo_path ]] && ! (( continue )); then
		# This error's language follows that given by `git rebase` when a rebase is already in progress
		__git_replay_print_error "fatal: It seems that there is already a replay todo file, and\nI wonder if you are in the middle of another replay.  If that is the\ncase, please try\n        git replay [--file <file>] (--continue | --abort | --skip)\nIf that is not the case, please\n        rm \".git/$dir_name/git-replay-todos\"\nand run me again.  I am stopping in case you still have something\nvalueable there."
	fi

	if (( dediverge )); then
		__git_replay_dediverge
		__git_replay_print_log Replay complete
		return
	fi

	mkdir -p $dir
	touch $todo_path

	(( queue_rebases )) && __git_replay_write_rebase_todos
	(( queue_rebase_ontos )) && __git_replay_write_rebase_onto_todos
	(( queue_stages )) && __git_replay_write_staging_todos

	todos=( ${(f)"$('command' 'cat' $todo_path)"} )
	for todo in $todos; do
		__git_replay_do_todo $todo
		__git_replay_pop_todo
	done

	__git_replay_pop_todo
	rm $todo_path

	__git_replay_print_log Replay complete
	__git_replay_warnings && rm $warning_path

	[[ -f $todo_path || -f $warning_path ]] || __git_replay_abort
}

__git_replay_merge_continue() {
	local branch
	local commit
	local -i the_exit_code

	branch=$2
	commit=$1
	the_exit_code=$3

	if [[ $the_exit_code == 128 || ! $(git config rerere.enabled) == true ]]; then
		__git_replay_print_error "Cannot continue"
	fi

	if [[ -n $(git rerere remaining) ]]; then
		todos_updated=$(mktemp ${TMPDIR:-/tmp/}git-replay/git-replay-todos_updated.XXXXXX)
		'builtin' 'echo' "merge-continue $commit $branch" > $todos_updated
		'command' 'cat' $todo_path >> $todos_updated
		'command' 'mv' $todos_updated $todo_path

		__git_replay_log_divergence $branch

		# This error's langage follows that given when `git rebase` hits a conflict.
		__git_replay_print_error 'Resolve all conflicts manually, mark them as resolved with\n"git add/rm <conflicted_files>", then run "git replay [--file <config file>] --continue".\nYou can instead skip this replay action: run "git replay [--file <config file>] --skip".\nTo abort and get back to the state before "git replay", run "git replay --abort".'
	fi

	__git_replay_print_log "All conflicts resolved by git-rerere"

	conflicted_files=$(git diff --name-only --diff-filter=U)

	if [[ -n $conflicted_files ]]; then
		__git_replay_print_log "Adding rerere-resolved conflicted files"
		(( quiet )) || 'builtin' 'print' "git add $conflicted_files"
		git add $conflicted_files
	fi

	(( quiet )) || 'builtin' 'print' "git commit --no-edit"
	git commit --no-edit || __git_replay_print_error "Cannot continue"
}

__git_replay_pop_todo() {
	todos_updated=$(mktemp ${TMPDIR:-/tmp/}git-replay/git-replay-todos_updated.XXXXXX)
	'command' 'sed' '1d' $todo_path > $todos_updated
	'command' 'mv' $todos_updated $todo_path
}

__git_replay_print_error() {
	__git_replay_warnings
	'builtin' 'print' -P "$color[error]$@%f"
	'builtin' 'exit' 1
}

__git_replay_print_log() {
	(( quieter )) || 'builtin' 'print' -P "$color[log]$@%f"
}

__git_replay_print_success() {
	(( quieter )) || 'builtin' 'print' -P "$color[success]$@%f"
}

__git_replay_print_warning() {
	(( quieter )) || 'builtin' 'print' -P "$color[warning]$@%f"
}

__git_replay_rebase_continue() {
	local branch
	local -i the_exit_code
	local newbase
	local upstream

	branch=$3
	the_exit_code=$1
	newbase=$4
	upstream=$2

	if [[ $the_exit_code == 128 || ! $(git config rerere.enabled) == true ]]; then
		__git_replay_print_error "Cannot continue"
	fi

	if [[ -n $(git rerere remaining) ]]; then
		todos_updated=$(mktemp ${TMPDIR:-/tmp/}git-replay/git-replay-todos_updated.XXXXXX)
		'builtin' 'echo' "rebase-continue $upstream $branch $newbase" > $todos_updated
		'command' 'cat' $todo_path >> $todos_updated
		'command' 'mv' $todos_updated $todo_path

		__git_replay_log_divergence $branch

		# This error's langage follows that given when `git rebase` hits a conflict.
		__git_replay_print_error 'Resolve all conflicts manually, mark them as resolved with\n"git add/rm <conflicted_files>", then run "git replay [--file <config file>] --continue".\nYou can instead skip this replay action: run "git replay [--file <config file>] --skip".\nTo abort and get back to the state before "git replay", run "git replay --abort".'
	fi

	__git_replay_print_log "All conflicts resolved by git-rerere"

	conflicted_files=$(git diff --name-only --diff-filter=U)

	if [[ -n $conflicted_files ]]; then
		__git_replay_print_log "Adding rerere-resolved conflicted files"
		(( quiet )) || 'builtin' 'print' "git add $conflicted_files"
		git add $conflicted_files
	fi

	(( quiet )) || 'builtin' 'print' "git commit --no-edit && git rebase --continue"
	git commit --no-edit || __git_replay_print_error "Cannot continue"
	git rebase --continue || __git_replay_rebase_continue $? $upstream $branch $newbase
}

__git_replay_warnings() {
	[[ -f $warning_path ]] || return

	local -a __git_replay_warnings

	__git_replay_warnings=( ${(f)"$('command' 'cat' $warning_path)"} )
	'builtin' 'typeset' -U __git_replay_warnings

	for warning in $__git_replay_warnings; do
		__git_replay_print_warning $warning
	done
}

__git_replay_dediverge() {
	local branch
	local -a branches
	local diverged_branch
	local -a diverged_branches
	local -a rebase_branches
	local rebase_branch
	local rebase_branch_value
	local -a rebase_branch_values
	local rebase_onto_upstream
	local rebase_onto_upstream_path
	local -a rebase_onto_upstream_paths
	local rebase_onto_branch
	local -a rebase_onto_branches
	local rebase_onto_newbase
	local rebase_onto_newbase_path
	local -a rebase_onto_newbase_paths
	local rebase_upstream
	local rebase_upstream_path
	local -a rebase_upstream_paths
	local stage_branch
	local stage_branch_path
	local -a stage_branch_paths
	local -a stage_branches
	local stage_start_point
	local stage_start_point_path
	local -a stage_start_point_paths

	__git_replay_print_log Looking for diverged branches...

	# get all branches in rebase commands
	rebase_upstream_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'rebase.*')"} )
	for rebase_upstream_path in $rebase_upstream_paths; do
		rebase_branch_values=( ${(f)"$('command' 'yq' r --stripComments $file $rebase_upstream_path)"} )

		for rebase_branch_value in $rebase_branch_values; do
			rebase_branch=${rebase_branch_value#- }
			branches+=( $rebase_branch )
		done
	done

	# get all branches in rebase-onto commands
	rebase_onto_newbase_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'rebase-onto.*')"} )
	for rebase_onto_newbase_path in $rebase_onto_newbase_paths; do
		rebase_onto_newbase=${rebase_onto_newbase_path#rebase-onto.}
		rebase_onto_upstream_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file $rebase_onto_newbase_path.\*)"} )

		for rebase_onto_upstream_path in $rebase_onto_upstream_paths; do
			rebase_onto_upstream=${rebase_onto_upstream_path#$rebase_onto_newbase_path.}
			branches+=( ${(f)"$('command' 'yq' r $file $rebase_onto_upstream_path.\*)"} )
		done
	done

	# get all branches in stage commands
	stage_start_point_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'stage.*')"} )
	for stage_start_point_path in $stage_start_point_paths; do
		stage_start_point=${stage_start_point_path#stage.}
		stage_branch_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file $stage_start_point_path.\*)"} )

		for stage_branch_path in $stage_branch_paths; do
			stage_branch=${stage_branch_path#$stage_start_point_path.}
			branches+=( $stage_branch )
		done
	done

	# identify diverged branches
	for branch in $branches; do
		(( ${diverged_branches[(Ie)$branch]} )) && continue
		git checkout --quiet $branch || continue
		__git_replay_diverged $branch || continue
		diverged_branches+=( $branch )
	done

	if ! (( ${#diverged_branches} )); then
		(( quieter )) || 'builtin' 'echo' All branches in your git-replay configuration are up to date
		return
	fi

	if ! (( quieter )); then
		'builtin' 'echo' \\nThe following branches have diverged from their remote tracking branches:
		for diverged_branch in $diverged_branches; do
			'builtin' 'echo' $diverged_branch
		done

		'builtin' 'echo'
	fi

	# handle diverged branches
	if ! (( dry_run )); then
		for diverged_branch in $diverged_branches; do
			if ! (( force_dediverge )); then
				if ! read -q "REPLY?Force push \`$diverged_branch' with lease? [y/Y to accept, any other key to skip] "; then
					'builtin' 'echo'
					continue
				fi

				'builtin' 'echo'
			fi

			if (( quiet )); then
				git checkout --quiet $diverged_branch
				git push --force-with-lease --quiet
			else
				'builtin' 'print' "git checkout --quiet $diverged_branch"
				git checkout --quiet $diverged_branch
				'builtin' 'print' "git push --force-with-lease --quiet"
				git push --force-with-lease --quiet
			fi
		done
	fi
}

__git_replay_write_rebase_todos() {
	local branch
	local branch_value
	local -a branch_values
	local the_exit_code
	local upstream
	local upstream_path
	local -a upstream_paths

	upstream_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'rebase.*')"} )
	for upstream_path in $upstream_paths; do
		branch_values=( ${(f)"$('command' 'yq' r --stripComments $file $upstream_path)"} )
		upstream=${upstream_path#rebase.}

		if ! git show-ref --verify --quiet refs/heads/$upstream; then
			# This error's language follows that of `git rebase --onto <a nonexistent ref> <upstream> <branch>`
			'builtin' 'print' "fatal: invalid upstream '$upstream'\n"

			rm $todo_path 2>/dev/null
			__git_replay_print_error "There is a problem in the 'rebase.*' configuration.\nFix it and run me again."
		fi

		for branch_value in $branch_values; do
			branch=${branch_value#- }

			if ! git show-ref --verify --quiet refs/heads/$branch; then
				# This error's language follows that of `git rebase --onto <a nonexistent ref> <upstream> <branch>`
				'builtin' 'print' "fatal: fatal: no such branch/commit '$branch'\n"

				rm $todo_path 2>/dev/null
				__git_replay_print_error "There is a problem in the 'rebase.$upstream.*' configuration.\nFix it and run me again."
			fi

			'builtin' 'echo' "rebase ${upstream_path#rebase.} $branch" >> $todo_path
		done
	done
}

__git_replay_write_rebase_onto_todos() {
	local upstream
	local upstream_path
	local -a upstream_paths
	local branch
	local -a branches
	local newbase
	local newbase_path
	local -a newbase_paths

	newbase_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'rebase-onto.*')"} )

	for newbase_path in $newbase_paths; do
		newbase=${newbase_path#rebase-onto.}

		if ! git show-ref --verify --quiet refs/heads/$newbase; then
			# This error's language follows that of `git rebase --onto <a nonexistent ref> <upstream> <branch>`
			'builtin' 'print' "fatal: Does not point to a valid commit '$newbase'\n"

			rm $todo_path 2>/dev/null
			__git_replay_print_error "There is a problem in the 'rebase-onto.*' configuration.\nFix it and run me again."
		fi

		upstream_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file $newbase_path.\*)"} )

		for upstream_path in $upstream_paths; do
			upstream=${upstream_path#$newbase_path.}

			if ! git show-ref --verify --quiet refs/heads/$upstream; then
				# This error's language follows that of `git rebase --onto <a nonexistent ref> <upstream> <branch>`
				'builtin' 'print' "fatal: invalid upstream '$upstream'\n"

				rm $todo_path 2>/dev/null
				__git_replay_print_error "There is a problem in the 'rebase-onto.$newbase.*' configuration.\nFix it and run me again."
			fi

			branches=( ${(f)"$('command' 'yq' r $file $upstream_path.\*)"} )

			for branch in $branches; do
				if ! git show-ref --verify --quiet refs/heads/$branch; then
					# This error's language follows that of `git rebase --onto <a nonexistent ref> <upstream> <branch>`
					'builtin' 'print' "fatal: fatal: no such branch/commit '$branch'\n"

					rm $todo_path 2>/dev/null
					__git_replay_print_error "There is a problem in the 'rebase-onto.$newbase.$upstream.*' configuration.\nFix it and run me again."
				fi

				'builtin' 'echo' "rebase-onto $newbase $upstream $branch" >> $todo_path
			done
		done
	done
}

__git_replay_write_staging_todos() {
	local branch
	local branch_path
	local -a branch_paths
	local commit
	local -a commits
	local start_point
	local start_point_path
	local -a start_point_paths

	start_point_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file 'stage.*')"} )

	for start_point_path in $start_point_paths; do
		start_point=${start_point_path#stage.}

		if ! git show-ref --verify --quiet refs/heads/$start_point; then
			# This error's language follows that of `git reset --hard <a nonexistent ref>`
			'builtin' 'print' "fatal: ambiguous argument '$start_point': unknown revision.\n"

			rm $todo_path 2>/dev/null
			__git_replay_print_error "There is an error in the 'stage.*' configuration.\nFix it and run me again."
		fi

		branch_paths=( ${(f)"$('command' 'yq' r --stripComments --printMode p $file $start_point_path.\*)"} )

		for branch_path in $branch_paths; do
			branch=${branch_path#$start_point_path.}
			commits=( ${(f)"$('command' 'yq' r $file $branch_path.\*)"} )
			'builtin' 'echo' "stage-start $branch $start_point" >> $todo_path

			for commit in $commits; do
				if ! git show-ref --verify --quiet refs/heads/$commit; then
					# This error's language follows that of `git merge --no-ff <a nonexistent ref>`
					'builtin' 'print' "merge: $commit - not something we can merge\n"

					rm $todo_path 2>/dev/null
					__git_replay_print_error "There is a problem in the 'stage.$branch.*' configuration.\nFix it and run me again."
				fi

				'builtin' 'echo' "merge $commit $branch" >> $todo_path
			done

			'builtin' 'echo' "stage-end $branch" >> $todo_path
		done
	done
}

emulate -L zsh

'builtin' 'setopt' prompt_percent

root=$(git rev-parse --show-toplevel 2>/dev/null)
if [[ -z $root ]]; then
	__git_replay_print_log "Not a Git repo"
	return
fi

readonly SOURCE_PATH=${0:A:h}
readonly BACKUP_PREFIX="git-replay"
readonly VERSION=2.2.1

mkdir -p ${TMPDIR:-/tmp/}git-replay

dir_name=replay
dir=$root/.git/$dir_name
todo_path=${dir}/git-replay-todos
warning_path=${dir}/git-replay-warnings
action_path=${dir}/git-replay-current-action

file=$root/git-replay.yaml

'builtin' 'typeset' -i back_up
'builtin' 'typeset' -A color
'builtin' 'typeset' -i dry_run
'builtin' 'typeset' -i quiet
'builtin' 'typeset' -i quieter
'builtin' 'typeset' -a __git_replay_warnings

__git_replay_main $@
